// ---------------------------------------------------------------------------------------------- //
// Aplikace pro Farní evangelizační buňky - jednoduché CMS a DBS                                  //
//                                                                                                //
// Ezer3                                                (c) 2018 Martin Šmídek <martin@smidek.eu> //
// ---------------------------------------------------------------------------------------------- //
var dbg: object
menu feb { title:'feb', type:'main', active:*, _sys:'*'
  tabs ora  {title:"[fa-bell-o] Buňky", _sys:'*', include:'onload', active:* 
  }
  tabs doc  {title:"Nápověda", _sys:'*',  include:'onclick,ezer2.help' active:*, skill:'ah' }
  tabs syst {title:"Systém",  include:'onclick,ezer2.syst', _sys:'sys'
    panel pers {type:'right', title:'Osobní nastavení', _sys:'*', include:'onload,ezer2.pers'}
    # ===================================================================================== DATABÁZE
    panel dat {type:'right', title:'[fa-database] Databáze', _sys:'*', include:'onload,ezer3.db', skill:'a'
      par:{
        infos: [
          {title:'[fa-object-group] Schema databáze',
           html: "Zjednodušené schema databáze pro <b>FEB</b><br><br>
                <img src='feb/img/schema_db_feb_2020.png' />"}
        ],
        tables: {
          lidi:   "id_lidi>*,prijmeni|entita L-LIDI",
          akce:   "id_akce>*,nazev,zacatek|entita A-AKCE",
          pack:   "id_pack>*,pack_date,pack_subj|entita P-PACK",
          cell:   "id_cell>*,nazev|entita C-CELL",
          fara:   "id_fara>*,obec,nazev|entita F-FARA",
          na:   "id_na>*,id_lidi>lidi,id_akce>akce,poznamka|relace NA:L-A",
          go:   "id_go>*,id_lidi>lidi,id_pack>pack,msg|relace GO:L-P",
          ma:   "id_ma>*,id_lidi>lidi,id_cell>cell,poznamka|relace MA:L-C",
          je:   "id_je>*,id_lidi>lidi,id_fara>fara,funkce|relace JE:L-F",
          ve:   "id_ve>*,id_cell>cell,id_fara>fara,poznamka|relace VE:C-F",
          menu:   "mid>*,mid_top>menu,mid_sub>menu,nazev,elem>man_append|web MENU",
          clanek: "id_clanek>*,web_text"
        },
        css: 'systable' // styl tabulek
      }
      func append(x) {}
      func man_append(tab,val) { var x:text
        x= php.man_append(tab,val);
        append(x);
      }
    }
  }
  tabs off  {title:"[fa-power-off] Odhlásit", type:'logoff' }
}
// univerzální formulář pro levostranné menu
form right [,,700,50] {
  label info [0,0,680,600] { title:'' }
  label head [0,0,680,50]  { title:'' }
  label note [0,50,680,550] { title:'' }
  proc fill(x,y) {
    [ x; head.set(conc("<div class='karta'>",x,"</div>")) ];
    [ y; note.set(y) ]
  }
  proc append(x) { note.get; note.set(conc(note.get,'<br>',x)) | note.set(x) }
}
# ==============================================================================> EDITOR
panel EDITOR [0,0,710,530] { title:' Úprava textu', type:'popup'
  var id:number,
      path:text
  use a: form _a [0,0,,] { tag:'a', format:'n' }
  use b: form _b [0,0,,] { tag:'b', format:'n' }
  use c: form _c [0,0,,] { tag:'c', format:'n' }
  use d: form _d [0,0,,] { tag:'d', format:'n' }
  proc Akce(_key) {
    id.set(_key);
    panel.display(2,'a'); 
    a.load(_key); 
    path.set(conc('/inc/a/',_key));                  // cesta pro přílohy
    ask('session','feb,inc_path',path.get);
    join_drop(a.obsah);
    [ panel.modal(80,10) ]
  }
  proc Bunka(_key) {
    id.set(_key);
    panel.display(2,'b'); 
    b.load(_key); 
    path.set(conc('/inc/b/',_key));                  // cesta pro přílohy
    ask('session','feb,inc_path',path.get);
    join_drop(b.obsah);
    [ panel.modal(80,10) ]
  }
  proc Clanek(_key) {[
    id.set(_key);
    panel.display(2,'c'); 
    c.load(_key); 
    path.set(conc('/inc/c/',_key));                  // cesta pro přílohy
    ask('session','feb,inc_path',path.get);
    join_drop(c.obsah);
    panel.modal(80,10) 
  ]}
  proc Pack(_key) {[
    id.set(_key);
    panel.display(2,'d'); 
    d.load(_key); 
    path.set(conc('/inc/d/',_key));                  // cesta pro přílohy
    ask('session','feb,inc_path',path.get);
    join_drop(d.obsah);
    panel.modal(80,10) 
  ]}
  proc join_drop(obsah) {
    UPLOAD.ckeditor.set(obsah);
    UPLOAD.path.set(path.get);
    UPLOAD.g.drop.Init;
    [ function('edit','drop',"edit.label_drop=drop; return 1;",obsah,UPLOAD.g.drop) ]
  }
  # -------------------------------------------------------------------------------==> edit akce    
  # oprava textu akce
  form _a [10,10,710,460] {
    button  [-121,5,,] { title:'Uložit', help:'ukončit editor a uložit změny'
      proc onclick() { form.save; panel.hide(1); }}
    button  [-75,5,,] { title:'Zpět', help:'ukončit editor bez uložení změn'
      proc onclick() { panel.hide(0); }}
    field [39,10,190,] { title:'menu:', data:akce.web_menu }
    label [245,13,55,20] { title:'zobrazit?' }
    radio [303,8,150,20] { data:akce.web_stav
      case [0,0,50,] { title:'ne', expr:'0' }
      case [50,0,50,] { title:'zde', expr:'1' }
      case [100,0,50,] { title:'web', expr:'2' }
      proc onchange () {
    }} 
    check [460,10,82,20] { title:'přihláška:', data:akce.web_prihl }
    button app [-3,4,,] { title:'[fa-files-o] Přílohy', help:'seznam souborů sdružených s textem'
      proc onclick() {
        UPLOAD.ckeditor.set(obsah);
        UPLOAD.path.set(path.get);
        UPLOAD.modal //(50,100)
      }
    }
    edit obsah [0,40,710,480] { type:'html', data:akce.web_text, par:°{toolbar:'FEB'} 
      proc ondrop(f) {                   // odmítnutí přenosu při novém článku
        echo('CKEDIT dropped=',f.name);  //  kterého neznáme cid
        [ form.key
        | warning("obrázky lze vkládat jen do opravovaného článku") ];
        return(form.key);
      }
    }
  }
  # -------------------------------------------------------------------------------==> edit buňka    
  # oprava textu buňky
  form _b [10,10,710,460] {
    button  [-121,5,,] { title:'Uložit', help:'ukončit editor a uložit změny'
      proc onclick() { form.save; panel.hide(1); }}
    button  [-75,5,,] { title:'Zpět', help:'ukončit editor bez uložení změn'
      proc onclick() { panel.hide(0); }}
    field [50,10,200,] { title:'menu:', data:cell.web_menu }
    label [277,15,150,20] { title:'zobrazit?' }
    radio [330,10,150,20] { data:cell.web_stav
      case [0,0,50,] { title:'ne', expr:'0' }
      case [50,0,50,] { title:'zde', expr:'1' }
      case [100,0,50,] { title:'web', expr:'2' }
      proc onchange () {
    }} 
    button app [-3,4,,] { title:'[fa-files-o] Přílohy', help:'seznam souborů sdružených s textem'
      proc onclick() {
        UPLOAD.ckeditor.set(obsah);
        UPLOAD.path.set(path.get);
        UPLOAD.modal //(50,100)
      }
    }
    edit obsah [0,40,710,480] { type:'html', data:cell.web_text, par:°{toolbar:'FEB'} 
      proc ondrop(f) {                   // odmítnutí přenosu při novém článku
        echo('CKEDIT dropped=',f.name);  //  kterého neznáme cid
        [ form.key
        | warning("obrázky lze vkládat jen do opravovaného článku") ];
        return(form.key);
      }
    }
  }
  # -----------------------------------------------------------------------------==> edit clanek    
  # oprava textu článku
  form _c [10,10,710,460] {
    button  [-121,5,,] { title:'Uložit', help:'ukončit editor a uložit změny'
      proc onclick() { form.save; panel.hide(1); }}
    button  [-75,5,,] { title:'Zpět', help:'ukončit editor bez uložení změn'
      proc onclick() { panel.hide(0); }}
    button app [-3,4,,] { title:'[fa-files-o] Přílohy', help:'seznam souborů sdružených s textem'
      proc onclick() {
        UPLOAD.ckeditor.set(obsah);
        UPLOAD.path.set(path.get);
        UPLOAD.modal //(50,100)
      }
    }
    edit obsah [0,40,710,480] { type:'html', data:clanek.web_text, par:°{toolbar:'FEB'} 
      proc ondrop(f) {                   // odmítnutí přenosu při novém článku
        echo('CKEDIT dropped=',f.name);  //  kterého neznáme cid
        [ form.key
        | warning("obrázky lze vkládat jen do opravovaného článku") ];
        return(form.key);
      }
    }
  }
  # -----------------------------------------------------------------------------==> edit mail
  # oprava textu mailu
  form _d [10,10,710,460] {
    button  [-121,5,,] { title:'Uložit', help:'ukončit editor a uložit změny'
      proc onclick() { form.save; panel.hide(1); }}
    button  [-75,5,,] { title:'Zpět', help:'ukončit editor bez uložení změn'
      proc onclick() { panel.hide(0); }}
    edit obsah [0,40,710,480] { type:'html', data:pack.pack_text, par:°{toolbar:'FEB'} 
      proc ondrop(f) {                   // odmítnutí přenosu při novém mailu
        echo('CKEDIT dropped=',f.name);  //  kterého neznáme cid
        [ form.key
        | warning("obrázky lze vkládat jen do opravovaného mailu") ];
        return(form.key);
      }
    }
  }
}
# ----------------------------------------------------------------------------==> UPLOAD
panel UPLOAD [0,0,400,130] { title:' Nahrát soubor', type:'popup'
  var ckeditor:object,
      path:text
  use g: form _g [10,0,,]
  proc onfocus() {
    g.drop.loaded
  }
  form _g [,,400,100] {
    label dlab [0,10,380,] {tag:'l_', title:'... použij [init] ' }
    label drop [0,27,380,100] {tag:'l_', type:'drop', 
        par:°{contextmenu:"-vložit obrázek;vložit odkaz"}
      proc ondrop(f) {                                                // možnost odmítnutí přenosu
        //gt(f.size,5242880); warning('odmítnuto ',f.name,' má více jak 5MB'); return(0)
        echo('dropped=',f.name);                                      // jméno nemusí být ASCII
        return(f.name);
      }
      proc onload(f) { echo("loaded=",f.name) } // po dokončení přenosu, jméno bude ASCII
      proc onerror(e) { // funkce onerror nesmí obsahovat asynchronní kód a lokální proměnné
        echo("drop error=",e.msg); return(1) // vrácení 0 způsobí standardní hlášení chyby
      }
      proc onmenu(op,name,ref) { var name2:text
        eq(op,'remove'); echo(ask('drop_unlink',path.get,name,'S:')); loaded
      | eq(op,'rename'); [
          name2.set(prompt2("zadej nové jméno pro ",name)); 
          name2;
          echo(ask('drop_rename',path.get,name,name2,'S:')); loaded
        ]
      | eq(op,'remove-all'); echo(ask('drop_unlink',path.get,'*','S:')); loaded
      | eq(op,'-vložit obrázek');
        { not(ckeditor.get); warning("to lze jen v prostředí otevřeného editoru");
        | [ function('t','n',
              "t.ckeditor.insertHtml('<img src=\"'+n+'\"/>');",
              ckeditor.get,conc(path.get,'/',name)) ];
        }
      | eq(op,'vložit odkaz');
        { not(ckeditor.get); warning("to lze jen v prostředí otevřeného editoru");
        | function('t',"return t.ckeditor.getSelection().getSelectedText();",ckeditor.get);
          [ function('t','n',
              "t.ckeditor.insertHtml('<a target=\"doc\" href=\"'+n+'\">'
               +t.ckeditor.getSelection().getNative()+'</a>');",
              ckeditor.get,conc(path.get,'/',name)) ];
          panel.hide(1)
        | panel.hide(0);
          alert("pro vložení odkazu na soubor je třeba napřed označit text, na který se má klikat")
        }
      }
      proc Init() { drop.init(path.get,'S:'); }
      proc loaded() {
        Init;
        dlab.set(conc('soubory pro článek (',path.get,')'));
        echo('lsdir=',drop.lsdir)
      }
    }
    button [-14,2,,] { title:"[fa-cloud-download] Vložit soubor odkazem"
      proc onclick() { var url:text, ret:object
        url.set(UPLOAD_URL.modal);
        url; ret.set(ask('upload_url',url,EDITOR.id.get));
        ret.err; alert(ret.err)
      | url; g.drop.loaded
    }}
  }
  panel UPLOAD_URL [,,300,60] { type:'popup' title:'zadej url s přílohou' use ff:form _ff
    proc onfocus() { ff.zip.init; ff.zip.focus }
    form _ff {
      field zip [0,0,300,] { format:'t' }
      button [0,30,,] { title:"[fa-save] Přidej přílohu"
        proc onclick() { panel.hide(zip.get) } }
      button [100,30,,] { title:"[fa-undo] Zpět"
        proc onclick() { panel.hide(0) } }
    }
  }
}
# ----------------------------------------------------------------------------==> TRACK
# přehled provedených změn
panel track [,,540,276] { title:'Historie oprav', type:'popup', css:'dialog'
  var reverted_table= '', reverted_key= 0
  use back: form _back [0,16,,] { tabindex:20 },
  proc back_show(kde,klic) {
    panel.popup(0,1);
    panel.set_attrib('title',conc('Historie oprav pro ',kde,' id=',klic));
    reverted_table.set(''); reverted_key.set(0);
    back.lst.browse_load(conc("(kde='",kde,"' AND klic=",klic,")"),"kdy DESC");
    back.lst.raise('onrowclick')
  }
  # ------------------------------------------------------------- _back
  # id_track,kdy,kdo,kde,klic,zmena:fld,op,val,old
  form _back [,,255,250] {
    browse lst [0,0,150,100] { rows:12,
      show kdy [,,90,] { title:'kdy', data:_track.kdy, sql_pipe:'sql_time1' }
      show kdn [,,30,] { title:'kdn', data:_track.kdo }
      show op [,,12,] { title:'?', data:_track.op }
      show kde [,,40,] { title:'tabulka', data:_track.kde }
      show fld [,,60,] { title:'položka', data:_track.fld }
      show old [,,160,] { title:'původní hodnota', data:_track.old, format:'t' }
      show val [,,120,] { title:'změněná hodnota', data:_track.val, format:'t' }
      show id_track [,,0,] { data:_track.id_track }
      menu { type:'context'
        item { title:'vrátit nastavenou opravu'
          proc onclick() {
            lst.selected('clear'); lst.selected('toggle',1); revert }}
        item { title:'vrátit i související opravy'
          proc onclick() { var ret:object
            lst.selected('clear');
            ret.set(ask('track_like',lst.browse_key));
            { ret.ok; lst.selected('set',ret.ids); [ revert ]
            | alert(ret.msg) }
          }}
      }
      proc revert() { var ret:object
        confirm('mám zkusit vrátit opravy označených řádků?');
        ret.set(ask('track_revert',lst.selected('get',0)));
        { ret.ok; warning("změny byly vráceny");
          reverted_table.set(ret.tab); reverted_key.set(ret.klic);
          lst.browse_refresh;
        | alert(ret.msg) }
      }
    },
    label [0,250,545,] { title:"ÚDAJE: i=vložení (insert), u=úprava, U= hromadná úprava<div style='float:right'>V=vrácení spojených záznamů</div><br>ZÁZNAM: x=smazano, o=obnoveno, d=vzniklo spojením duplicitních záznamů, r=různé od uvedeného záznamu" }
  }
}
# ----------------------------------------------------------------------------==> TISK
# pohlížní a tisk sestav
# par = typ hdr {sel|cnd} tit fld
panel TISK [,,840,400] { type:'popup', css:'dialog'
  var parm:object
  use t: form _t [0,0,,]
  proc do(_par) {
    panel.popup(50,100);
    panel.set_attrib('title',_par.hdr);
    t.tt.set(ask('feb_sestava',_par));
    parm.set(_par);
  }
  form _t [,,850,400] {
    label tt [0,0,845,400] { style:"overflow:auto" }
    label  [-25,0,48,26] { css:'parm' }
    button [-29,4,,] { title:"Excel" 
      proc onclick(){ tt.set(ask('feb_sestava',parm.get,1)); }
    }
  }
}
# ----------------------------------------------------------------------------==> FARY
// číselník farností
panel FARY [0,0,327,220] { title:' Číselník farností', type:'popup', style:'z-index:2003'
  var goal:ezer
  func Show(_goal:ezer) { goal= _goal; panel.modal(22,202) }
  func onfirstfocus() { f.lst.browse_load(); }
  use f: form _f
  form _f {
    view cf: table _fary
    browse lst [0,0,,] { rows:8, qry_rows:1
      show id_fary { data:cf.id_fary }
      show id_geo { data:cf.id_geo }
      show ulice { data:cf.ulice }
      show telefony {data:cf.telefony}
      show email {data:cf.email}
      show nazev [,,110,] { title:'název', data:cf.nazev, format:'ts+q*' }
      show obec [,,150,] { title:'obec', data:cf.obec, format:'tsq*' }
      show psc [,,40,] { title:'PSČ', data:cf.psc, format:'tsq*'  }
      func onrowclick() { 
        copy_by_name(lst,goal);
      }
    }
    button [10,202,,] {title:"Přidej farnost"
      func onclick() { var geo:object
        if (lst.id_geo==0) {
          geo= php.geo_mapy_cz(lst.ulice,lst.obec);
          goal.id_geo= geo.id_geo;
          if (geo.id_geo) {
            php.query(`UPDATE _fary SET id_geo=${geo.id_geo} WHERE id_fary=${lst.id_fary}`);
          }
          else
            warning(`adresu '${lst.ulice},${lst.obec}' mapy.cz nepoznaly`);
        }
        panel.hide(1); 
      }
    }
    button [140,202,,] {title:"Znají ji mapy.cz?"
      func onclick() { var geo:object
        geo= php.geo_mapy_cz(lst.ulice,lst.obec);
        if (geo.id_geo) {
          goal.location= 'ok';
          warning(`ANO adresu '${lst.ulice},${lst.obec}' mapy.cz znají`);
        }
        else {
          goal.location= 'ko';
          warning(`NE adresu '${lst.ulice},${lst.obec}' mapy.cz nepoznaly`);
        }
      }
    }
    button [285,202,,] {title:"Zpět"
      func onclick() {
          panel.hide(1); 
      }
    }
  }
}

// <editor-fold defaultstate="collapsed" desc="++++++++++++++++++++++++++ FEB tables & maps">

# tabulky entit
table akce { key_id:'id_akce'
  number id_akce 
  text nazev  { help:'název|název akce' }
  date zacatek { sql_pipe:'sql_date1', help:'začátek|datum začátku akce' }
  date konec { sql_pipe:'sql_date1', help:'konec|datum ukončení akce' }
  number web_stav
  text web_menu
  text web_text
  number web_prihl  // 1: na webu je online přihláška
}
table lidi { key_id:'id_lidi'
  number id_lidi
  number deleted
  text jmeno  { help:'jméno' }
  text prijmeni  { help:'příjmení' }
  date narozeni { help:'narození|datum nebo 1.1.rok', sql_pipe:'sql_date1' } 
  number knez { help:'kněz|je kněz' } 
  text mail  { help:'mail' }
  text telefon  { help:'telefon' }
  text ulice { help:'ulice|bydliště - ulice, č.p.' }
  text psc { help:'psč|bydliště - psč' }
  text obec { help:'obec|bydliště - obec' }
  text adresa
  number stat { help:'stát|bydliště - stát' } // #stat
  date web_change { sql_pipe:'sql_date1', help:'změna|datum změny z webu' }
}
table fara { key_id:'id_fara'
  number id_fara
  number id_geo // poloha
  text nazev  { help:'název' }
  text ulice { help:'ulice|fara - ulice, č.p.' }
  text psc { help:'psč|fara - psč' }
  text obec { help:'obec|fara - obec' }
  text telefony { help:'telefony|fara - telefon' }
  text email { help:'email|fara - email' }
}
table cell { key_id:'id_cell'
  number id_cell
  text nazev  { help:'název' }
  text stav  { help:'stav' }
  number web_stav
  text web_menu
  text web_text
}
table pack { key_id:'id_pack'
  number id_pack
  date pack_date { sql_pipe:'sql_date1', help:'napsáno|datum napsání' }
  text pack_subj
  text pack_text
  text pack_encl
}

# tabulky relací
table na { key_id:'id_na' // lidi/akce
  number id_na
  number id_akce
  number id_lidi
  number funkce 
  text poznamka
  date web_change { sql_pipe:'sql_date1', help:'změna|datum změny z webu' }
}
table je { key_id:'id_je' // lidi/fara
  number id_je
  number id_fara
  number id_lidi
  number funkce 
}
table ma { key_id:'id_ma' // lidi/cell
  number id_ma
  number id_cell
  number id_lidi
  text poznamka
  number funkce 
}
table ve { key_id:'id_ve' // cell/fara
  number id_ve
  number id_fara
  number id_cell
  text poznamka
  number funkce 
}
table go { key_id:'id_go' // lidi/mail
  number id_go
  number id_lidi
  number id_pack
  number stav
}

# tabulky pro web
table clanek { key_id:'id_clanek'
  number id_clanek 
  text web_text
}
table menu { key_id:'id_menu' // 
  number id_menu
}

# tabulky systému, mapy
table _track { key_id:'id_track'
  number id_track { key:'primary' },
  date kdy { help:'datum změny' },
  text kdo { help:'pachatel' },
  text kde { help:'tabulka' },
  number klic { help:'klíč' },
  text fld { help:'položka' },
  text op { help:'operace' },
  text old { help:'původní hodnota' },
  text val { help:'nová hodnota' },
}
table _cis { key_id:'id_cis'
  number id_cis text druh, text data, text zkratka, number poradi,
  text hodnota, text popis, text barva, text ikona
}
table _geo { key_id:'id_geo'
  number id_geo 
  text adresa  // ulice, obec
  number lat
  number lng
  text zdroj // S|G
}
table _fary { key_id:'id_fary'
  number id_fary
  number id_geo // poloha
  text nazev  { help:'název' }
  text ulice { help:'ulice|fara - ulice, č.p.' }
  text psc { help:'psč|fara - psč' }
  text obec { help:'obec|fara - obec' }
  text telefony { help:'telefony|fara - telefon' }
  text email { help:'email|fara - email' }
}
map cis_stat:     table _cis {where:"druh='stat'"     order:'poradi' key_id:'data'}
map map_smtp_srv: table _cis {where:"druh='smtp_srv'" order:'poradi' key_id:'data'}
map map_funkce_n: table _cis {where:"druh='funkce_n'" order:'poradi' key_id:'data'}
map map_funkce_j: table _cis {where:"druh='funkce_j'" order:'poradi' key_id:'data'}
map map_funkce_m: table _cis {where:"druh='funkce_m'" order:'poradi' key_id:'data'}
map map_funkce_g: table _cis {where:"druh='funkce_g'" order:'poradi' key_id:'data'}
map map_funkce_v: table _cis {where:"druh='funkce_v'" order:'poradi' key_id:'data'}

// </editor-fold>